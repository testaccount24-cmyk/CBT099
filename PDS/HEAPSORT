 /* Do REXX Heap sort        */

 cc=HeapSort() /* Sorts stem 'stem.' */
  . . .

Heapsort: PROCEDURE expose stem.
 M = stem.0
 N = M
 do k=M % 2 to 1 by -1
   call DownHeap k N
 end /* do */
 do while N>1
   t = stem.1
   stem.1 = stem.n
   stem.n = t
   n = n-1
   call DownHeap 1 N
 end /* do */
 return 0
DownHeap: PROCEDURE expose stem.  /* subroutine of HeapSort */
 parse Arg k N
 v = stem.k
 do while k <= N%2
   j = k+k
   if j < n then do
     i = j+1
     if stem.j < stem.i then j=j+1
   end  /* do */
   if v >= stem.j then do
     stem.k = v
     RETURN
   end
   stem.k = stem.j
   k = j
 end /* do */
 stem.k = v
 return
